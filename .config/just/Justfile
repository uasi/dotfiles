import '~/.local/lib/just/prelude.just'
import? '_env.just'

# Rarely-used setup tasks.
mod setup

_default: __list__

# Create Justfile with imports.
[group('just')]
[no-cd]
init-justfile *imports:
    [ ! -f Justfile ]
    @for src in prelude {{ imports }}; do echo "import '~/.local/lib/just/$src.just'" >> Justfile; done
    @echo >> Justfile
    @echo '_default: __default__' >> Justfile
    @echo 'Created Justfile.'

# Clear irrelevant Cargo artifacts in ~/repos.
[group('housekeeping')]
cargo-sweep-repos:
    cargo sweep -i -r ~/repos

# Run `git fetch` for each repo in ~/repos.
[group('housekeeping')]
git-fetch-repos:
    git for-each-my-repo git fetch --all --prune --tags

# Run `git gc` for each repo in ~/repos.
[group('housekeeping')]
git-gc-repos:
    git for-each-my-repo git gc

# Clear global package caches of package managers for the npm ecosystem.
[group('housekeeping')]
clear-js-caches:
    @# Disable for now as Bun requires package.json to run `bun pm cache rm`.
    @# exec? bun pm cache rm
    exec? pnpm store prune
    exec? yarn cache clean

# Run all but Mac-related upgrade tasks at once.
[group('housekeeping')]
upgrade-full: upgrade-tools _upgrade-brew
    exec? brew upgrade

# Upgrade tools.
[group('housekeeping')]
upgrade-tools:
    exec? mise upgrade
    exec? rustup update

# Upgrade formulae and casks.
[group('housekeeping')]
_upgrade-brew:
    exec? brew upgrade

# Upgrade macOS.
[group('housekeeping')]
[confirm('May require GUI interaction, and after that, Mac will restart. Are you sure?')]
upgrade-mac-os:
    sudo softwareupdate --install --os-only --restart --agree-to-license

# Upgrade Mac App Store apps.
[group('housekeeping')]
[confirm('May require GUI interaction. Are you sure?')]
upgrade-mac-apps:
    exec? mas upgrade
